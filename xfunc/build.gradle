apply plugin: 'com.android.library'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 28

    lintOptions {
        abortOnError false
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly "com.github.rarnu:ktfunctional:0.8.3"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

group = 'com.github.rarnu'
version = '0.3.0'

signing {
    sign configurations.archives
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            pom.project {
                name project.name
                packaging 'aar'
                description 'xFunc'
                url 'https://github.com/rarnu/xfunc'

                scm {
                    url 'scm:git@github.com:rarnu/xfunc.git'
                    connection 'scm:git@github.com:rarnu/xfunc.git'
                    developerConnection 'git@github.com:rarnu/xfunc.git'
                }

                licenses {
                    license {
                        name 'MIT'
                        url 'https://github.com/rarnu/xfunc/blob/master/LICENSE'
                        distribution 'rarnu'
                    }
                }

                developers {
                    developer {
                        id 'rarnu'
                        name 'rarnu'
                        email '123909566@qq.com'
                    }
                }
            }
        }
    }
}
